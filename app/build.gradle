apply plugin: 'com.android.application'
apply plugin: 'com.xuexiang.xaop' //引用xaop插件

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.xuexiang.xuidemo"
        minSdkVersion 19
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    if (isNeedPackage.toBoolean()) {
        signingConfigs {
            release {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isNeedPackage.toBoolean()) {
                signingConfig signingConfigs.release
            }
        }
    }

    if (isNeedPackage.toBoolean()) {
        applicationVariants.all { variant ->
            variant.outputs.all {
                if (variant.buildType.name.equals('release')) {
                    outputFileName = "xuidemo_${defaultConfig.versionName}.apk"
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation deps.junit
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso.core

    //下拉刷新、CityPicker、XUI框架需要
    implementation deps.support.app_compat
    implementation deps.support.recyclerview
    implementation deps.support.design

    //悬浮按钮
    implementation 'com.github.clans:fab:1.6.4'

    //XUI框架
    implementation project(':xui_lib')
    implementation 'com.github.bumptech.glide:glide:4.8.0'

    //工具类
    implementation 'com.github.xuexiangjys.XUtil:xutil-core:1.1.5'
    implementation 'com.github.xuexiangjys.XUtil:xutil-sub:1.1.5'
    //切片
    implementation 'com.github.xuexiangjys.XAOP:xaop-runtime:1.0.1'

    //XPage页面框架
    implementation 'com.github.xuexiangjys.XPage:xpage-lib:2.2.4'
    annotationProcessor project(':widget_compiler')
    //ButterKnife
    implementation deps.butterknife.runtime
    annotationProcessor deps.butterknife.compiler
    //如果开启了内存泄漏监测leak,就需要加上这个依赖
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'

    //弹性布局
    implementation 'com.google.android:flexbox:0.3.1'

    //RxUtil2
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    //RxBinding的sdk
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.github.xuexiangjys:RxUtil2:1.1.5'

    //PhotoPicker图片选择
    implementation 'com.github.xuexiangjys:PhotoPicker:1.0.1'

    //下拉刷新
    implementation 'com.github.xuexiangjys.SmartRefreshLayout:refresh-header:1.1.5'
    implementation 'com.github.xuexiangjys.SmartRefreshLayout:refresh-layout:1.1.5'

    //XQRCode二维码扫描
    implementation 'com.github.xuexiangjys:XQRCode:1.0.2'
    //CityPicker
    implementation "com.github.xuexiangjys:CityPicker:1.0.0"


}
